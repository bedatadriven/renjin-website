repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    jbake
    sass
}

dependencies {
    jbake 'org.jbake:jbake-core:2.6.4'
    jbake 'args4j:args4j:2.33'
    jbake 'org.asciidoctor:asciidoctorj:1.5.8.1'
    jbake 'org.codehaus.groovy:groovy-templates:2.5.5'
    jbake 'org.codehaus.groovy:groovy-dateutil:2.5.5'
    jbake 'org.freemarker:freemarker:2.3.28'
    jbake 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'
    jbake 'de.neuland-bfi:jade4j:1.2.7'
    jbake 'com.vladsch.flexmark:flexmark:0.40.8'
    jbake 'com.vladsch.flexmark:flexmark-profile-pegdown:0.40.8'
    jbake 'org.eclipse.jetty:jetty-server:9.2.26.v20180806'
    jbake 'org.apache.commons:commons-vfs2:2.2'
    jbake 'org.slf4j:slf4j-simple:1.7.26'

    sass 'com.vaadin:vaadin-sass-compiler:0.9.13'
}

task compileSassPreview(type: JavaExec) {

    main = 'com.vaadin.sass.SassCompiler'
    classpath = configurations.sass

    inputs.dir 'src/sass/'
    outputs.file "$buildDir/preview/css/style.css"

    args 'src/sass/style.scss'
    args "$buildDir/preview/css/style.css"

    doFirst {
        project.file("$buildDir/output/css").mkdirs()
    }
}

task preview(type: JavaExec) {

    dependsOn 'compileSassPreview'

    main = 'org.jbake.launcher.Main'
    classpath = configurations.jbake

    args '--server'
    args 'src/jbake'
    args "$buildDir/preview"

    logging.captureStandardOutput LogLevel.INFO

    doFirst {
        println 'Open preview at http://localhost:8820/'
    }
}

task compileSass(type: JavaExec) {

    main = 'com.vaadin.sass.SassCompiler'
    classpath = configurations.sass

    args '-compress'
    args 'src/sass/style.scss'
    args "$buildDir/css/style.css"

    inputs.dir 'src/sass'
    outputs.file "$buildDir/css/style.css"

    doFirst {
        project.file("$buildDir/css").mkdirs()
    }
}

task bake(type: JavaExec) {

    main = 'org.jbake.launcher.Main'
    classpath = configurations.jbake

    main = 'org.jbake.launcher.Main'
    args 'src/jbake'
    args "$buildDir/jbake"

    inputs.dir 'src/jbake'
    outputs.dir "$buildDir/jbake"

    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.INFO
}


task stage(type: Copy) {
    dependsOn 'compileSass'
    dependsOn 'bake'

    into "$buildDir/staged"
    from "src/app"
    from("$buildDir/jbake") {
        into 'output'
    }
    from("$buildDir/css") {
        into 'output/css'
    }
}

task deploy(type: Exec) {
    dependsOn 'stage'
    
    executable 'gcloud'
    args '--project', 'renjinci'
    args 'app', 'deploy'
    args "$buildDir/staged"
    args "--version=1"
}